buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath group: "com.palantir", name: "gradle-baseline-java", version: gradleBaselineJavaVersion
        classpath group: "com.palantir.gradle.gitversion", name: "gradle-git-version", version: gradleGitVersionVersion
        classpath group: "net.ltgt.gradle", name: "gradle-apt-plugin", version: gradleAptPluginVersion
    }
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"

// Baseline plugins provide code style editor configuration and checking.
apply plugin: "com.palantir.baseline-config"
apply plugin: "com.palantir.baseline-checkstyle"
apply plugin: "com.palantir.baseline-eclipse"
apply plugin: "com.palantir.baseline-findbugs"
apply plugin: "com.palantir.baseline-idea"

// Git Version allows setting the product version based on Git tags.
apply plugin: "com.palantir.git-version"

// Annotations processing integration for Gradle.
apply plugin: "net.ltgt.apt"

sourceCompatibility = 1.8
version gitVersion()
group "com.aidanns"

repositories {
    mavenCentral()
    jcenter()
}

idea {
    module {
        // Add the directories for source files generated by the annotation processors to the classpath.
        sourceDirs += file("build/generated/source/apt/main")
        testSourceDirs += file("build/generated/source/apt/test")
    }
}

dependencies {
    compile group: "com.google.guava", name: "guava", version: guavaVersion
    compile group: "org.jdbi", name: "jdbi", version: jdbiVersion

    // Annotations are needed only at compile time.
    compileOnly group: "org.immutables", name: "value", version: immutablesVersion
    compileOnly group: "org.immutables", name: "builder", version: immutablesVersion
    compileOnly group: "org.immutables", name: "gson", version: immutablesVersion

    // Annotation processors are needed only at compile time.
    apt group: "org.immutables", name: "value", version: immutablesVersion

    testCompile group: "junit", name: "junit", version: junitVersion
}
